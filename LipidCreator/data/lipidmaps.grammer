##################################################################################
# MIT License
# 
# Copyright (c) 2017 Dominik Kopczynski   -   dominik.kopczynski {at} isas.de
#                    Bing Peng   -   bing.peng {at} isas.de
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
##################################################################################

## This is a BNF grammer for lipid subspecies identifiers followed by
## J.K. Pauling et al. 2017, PLoS One, 12(11):e0188394. 

# first rule is always start rule
lipid = lipid_mono | lipid_mono isotope
lipid_mono = lipid_pure | lipid_pure isoform
lipid_pure = Pure_FA | GL | PL | SL | Cholesterol | Mediator
isoform = square_open_bracket "rac" square_close_bracket | square_open_bracket "iso" Number square_close_bracket
isotope = " " round_open_bracket element Number round_close_bracket
element = "d"




# pure fatty acid
Pure_FA = FA





## glycerolipid rules
GL = SGL | TGL
SGL = HG_SGL round_open_bracket FA unsorted_FA_separator FA round_close_bracket | HG_SGL round_open_bracket FA sorted_FA_separator FA round_close_bracket
TGL = HG_GL round_open_bracket FA unsorted_FA_separator FA unsorted_FA_separator FA round_close_bracket | HG_GL round_open_bracket FA sorted_FA_separator FA sorted_FA_separator FA round_close_bracket
HG_GL = "MG" | "DG" | "TG"
HG_SGL = "MGDG" | "DGDG" | "SQDG" | "SQMG" | "DG"





## phospholipid rules
PL = LPL | DPL | CL | 4PL
LPL = HG_LPL round_open_bracket FA round_close_bracket
DPL = HG_PL round_open_bracket FA unsorted_FA_separator FA round_close_bracket | HG_PL round_open_bracket FA sorted_FA_separator FA round_close_bracket
CL = HG_CL round_open_bracket "1'-" square_open_bracket FA sorted_FA_separator FA square_close_bracket ",3'-" square_open_bracket FA sorted_FA_separator FA square_close_bracket round_close_bracket
4PL = HG_4PL round_open_bracket FA sorted_FA_separator FA sorted_FA_separator FA sorted_FA_separator FA round_close_bracket

HG_PL = HG_CL | HG_DPL PIP_position | HG_DPL | HG_LPL
HG_CL = "CL"
HG_DPL = "BMP" | "CDP-DG" | "DMPE" | "MMPE" | "PA" | "PC" | "PE" | "PEt" | "PG" | "PI" | "PIP" | "PIP2" | "PIP3" | "PS" | "PIM1" | "PIM2" | "PIM3" | "PIM4" | "PIM5" | "PIM6" | "Glc-DG" | "PGP" | "LBPA" | "PE-NMe2" | "AC2SGL" | "DAT" | "PE-NMe" | "PT" | "Glc-GP" | "NAPE"
HG_LPL = "LysoPC" | "LysoPE" | "LPIM1" | "LPIM2" | "LPIM3" | "LPIM4" | "LPIM5" | "LPIM6" | "CPA" | "LPA"
HG_4PL = "PAT16" | "PAT18"
PIP_position = square_open_bracket PIP_pos square_close_bracket
PIP_pos = PIP_pos "," PIP_pos | Number "'"





## sphingolipid rules
SL = LSL | DSL
LSL = HG_LSL headgroup_separator LCB
DSL = HG_DSL round_open_bracket LCB sorted_FA_separator FA round_close_bracket

HG_LSL = "LCB" | "LCBP" | "LHexCer" | "LSM"
HG_DSL = "Cer" | "CerP" | "EPC" | "GB3" | "GB4" | "GD3" | "GM3" | "GM4" | "Hex2Cer" | "HexCer" | "IPC" | "M(IP)2C" | "MIPC" | "SHexCer" | "SM" | "PE-Cer" | "PI-Cer" | "GlcCer" | "FMC-5" | "FMC-6" | "LacCer" | "GalCer" | "(3'-sulfo)Galbeta-Cer"






## cholesterol lipids
Cholesterol = Ch | ChE
Ch = "Cholesterol"
ChE = FA headgroup_separator HG_ChE
HG_ChE = "Cholesteryl ester"






## mediator lipids
Mediator = Mediator_Var | Mediator_Const
Mediator_Var = Mediator_Number "-" Mediator_Var_Name | "(+/-)-" Mediator_Number "-" Mediator_Var_Name
Mediator_Number = Mediator_Number Mediator_separator Mediator_Number | Number | Number Mediator_Pos | round_open_bracket Number round_close_bracket
Mediator_Pos = "R" | "S"
Mediator_Var_Name = "HHTrE" | "EpOME" | "HODE" | "HOTrE" | "DHET" | "EET" | "EpETE" | "HEPE" | "HETE" | "PGJ2" | "HDoHE" | "HpETE" | "OxoETE" | "DiHETE" | "LXA4"
Mediator_Const = "AA" | "alpha-LA" | "DHA" | "EPA" | "Linoleic acid" | "LTB4" | "LTC4" | "LTD4" | "Maresin 1" | "Palmitic acid" | "PGB2" | "PGD2" | "PGE2" | "PGF2alpha" | "PGI2" | "Resolvin D1" | "Resolvin D2" | "Resolvin D3" | "Resolvin D5" | "tetranor-12-HETE" | "TXB1" | "TXB2" | "TXB3"






## generic rules
FA = FA_unmod | FA_unmod FA_mod
FA_unmod = Ether FA_pure | FA_pure
FA_mod = round_open_bracket modification round_close_bracket
modification = modification "," modification | Number mod_text | mod_text
mod_text = mod_text mod_text | square_open_bracket mod_text square_close_bracket | "OH" | "Ke" | "S" | "OOH" | "Me" | "R" | "Br" | "CHO" | "COOH" | "Cp"
Ether = "P-" | "O-"
FA_pure = Carbon Carbon_DB_separator DB | Carbon Carbon_DB_separator DB DB_hydroxyl_separator Hydroxyl
LCB = Hydroxyl_LCB Carbon Carbon_DB_separator DB
Carbon = Number
DB = DB_count | DB_count round_open_bracket DB_position round_close_bracket
DB_count = Number
DB_position = Number | Number CisTrans | DB_position DB_position_separator DB_position
CisTrans = "E" | "Z"
Hydroxyl = Number
Hydroxyl_LCB = "m" | "d" | "t"
Number =  "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | Number Number




## separators
sorted_FA_separator = "/"
unsorted_FA_separator = "_"
headgroup_separator = " "
Carbon_DB_separator = ":"
DB_hydroxyl_separator = ";"
DB_position_separator = ","
Mediator_separator = ","
round_open_bracket = "("
round_close_bracket = ")"
square_open_bracket = "["
square_close_bracket = "]"

