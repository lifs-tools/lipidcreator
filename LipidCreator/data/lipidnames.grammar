##################################################################################
# MIT License
# 
# Copyright (c) 2017 Dominik Kopczynski   -   dominik.kopczynski {at} isas.de
#                    Bing Peng   -   bing.peng {at} isas.de
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
##################################################################################

## This is a BNF grammer for lipid subspecies identifiers followed by
## J.K. Pauling et al. 2017, PLoS One, 12(11):e0188394. 

# first rule is always start rule
lipid = lipid_pure | lipid_pure adductInfo
lipid_pure = GL | PL | SL | Cholesterol | MediatorC


# adduct information
adductInfo = "[M" adduct "]" charge charge_sign | adduct_separator "[M" adduct "]" charge charge_sign
adduct = "+H" | "+2H" | "+NH4" | "-H" | "-2H" | "+HCOO" | "+CH3COO" | adduct4 | adduct4 adduct4
adduct4 = adduct2 | adduct2 adduct2
adduct2 = Char | Char Char
Char = "a" | "b" | "c" | "d" | "e" | "f" | "g" | "h" | "i" | "j" | "k" | "l" | "m" | "n" | "o" | "p" | "q" | "r" | "s" | "t" | "u" | "v" | "w" | "x" | "y" | "z" | "A" | "B" | "C" | "D" | "E" | "F" | "G" | "H" | "I" | "J" | "K" | "L" | "M" | "N" | "O" | "P" | "Q" | "R" | "S" | "T" | "U" | "V" | "W" | "X" | "Y" | "Z" |"0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "+" | "-"
charge = "1" | "2" | "3" | "4"
charge_sign = "-" | "+"



## glycerolipid rules
GL = MGL | DGL | SGL | TGL
MGL = HG_MGL headgroup_separator FA
DGL = HG_DGL headgroup_separator FA unsorted_FA_separator FA | HG_DGL headgroup_separator FA sorted_FA_separator FA
SGL = HG_SGL headgroup_separator FA unsorted_FA_separator FA | HG_SGL headgroup_separator FA sorted_FA_separator FA
TGL = HG_TGL headgroup_separator FA unsorted_FA_separator FA unsorted_FA_separator FA | HG_TGL headgroup_separator FA sorted_FA_separator FA sorted_FA_separator FA

HG_MGL = "MAG"
HG_DGL = "DAG"
HG_SGL = "MGDG" | "DGDG" | "SQDG"
HG_TGL = "TAG"



## phospholipid rules
PL = LPL | DPL | PL-O | CL | MLCL
PL-O = LPL-O | DPL-O
LPL = HG_LPLc headgroup_separator FA
LPL-O = HG_LPL-Oc headgroup_separator FA
DPL = HG_PLc headgroup_separator FA unsorted_FA_separator FA | HG_PLc headgroup_separator FA sorted_FA_separator FA
DPL-O = HG_PL-Oc headgroup_separator FA unsorted_FA_separator FA | HG_PL-Oc headgroup_separator FA sorted_FA_separator FA
CL = HG_CLc headgroup_separator FA unsorted_FA_separator FA unsorted_FA_separator FA unsorted_FA_separator FA | HG_CLc headgroup_separator FA sorted_FA_separator FA sorted_FA_separator FA sorted_FA_separator FA
MLCL = HG_MLCLc headgroup_separator FA unsorted_FA_separator FA unsorted_FA_separator FA | HG_MLCLc headgroup_separator FA sorted_FA_separator FA sorted_FA_separator FA

HG_CLc = HG_CL | HG_CL heavy
HG_CL = "CL"
HG_MLCLc = HG_MLCL | HG_MLCL heavy
HG_MLCL = "MLCL"
HG_PLc = HG_PL | HG_PL heavy
HG_PL = "BMP" | "CDPDAG" | "DMPE" | "MMPE" | "PA" | "PC" | "PE" | "PEt" | "PG" | "PI" | "PIP" | "PIP2" | "PIP3" | "PS"
HG_LPLc = HG_LPL | HG_LPL heavy
HG_LPL = "LPA" | "LPC" | "LPE" | "LPG" | "LPI" | "LPS"
HG_LPL-Oc = HG_LPL-O | HG_LPL-O heavy
HG_LPL-O = "LPC O" | "LPE O"
HG_PL-Oc = HG_PL-O | HG_PL-O heavy
HG_PL-O = "PC O" | "PE O"



## sphingolipid rules
SL = LSL | DSL
LSL = HG_LSLc headgroup_separator LCB
DSL = HG_DSLc headgroup_separator LCB sorted_FA_separator FA

HG_LSLc = HG_LSL | HG_LSL heavy
HG_LSL = "LCB" | "LCBP" | "LHexCer" | "LSM"
HG_DSLc = HG_DSL | HG_DSL heavy
HG_DSL = "Cer" | "CerP" | "EPC" | "GB3" | "GB4" | "GD3" | "GM3" | "GM4" | "Hex2Cer" | "HexCer" | "IPC" | "M(IP)2C" | "MIPC" | "SHexCer" | "SM"



## cholesterol lipids
Cholesterol = Chc | ChE
Chc = Ch | Ch heavy
Ch = "Ch"
ChE = HG_ChEc headgroup_separator FA
HG_ChEc = HG_ChE | HG_ChE heavy
HG_ChE = "ChE"


## mediator lipids
MediatorC = Mediator | Mediator heavy
Mediator = "10-HDoHE" | "11-HDoHE" | "11-HETE" | "11,12-DHET" | "11(12)-EET"| "12-HEPE" | "12-HETE" | "12-HHTrE" | "12-OxoETE" | "12(13)-EpOME" | "13-HODE" | "13-HOTrE" | "14,15-DHET" | "14(15)-EET" | "14(15)-EpETE" | "15-HEPE" | "15-HETE" | "15d-PGJ2" | "16-HDoHE" | "16-HETE" | "18-HEPE" | "5-HEPE" | "5-HETE" | "5-HpETE" | "5-OxoETE" | "5,12-DiHETE" | "5,6-DiHETE" | "5,6,15-LXA4" | "5(6)-EET" | "8-HDoHE" | "8-HETE" | "8,9-DHET" | "8(9)-EET" | "9-HEPE" | "9-HETE" | "9-HODE" | "9-HOTrE" | "9(10)-EpOME" | "AA" | "alpha-LA" | "DHA" | "EPA" | "Linoleic acid" | "LTB4" | "LTC4" | "LTD4" | "Maresin 1" | "Palmitic acid" | "PGB2" | "PGD2" | "PGE2" | "PGF2alpha" | "PGI2" | "Resolvin D1" | "Resolvin D2" | "Resolvin D3" | "Resolvin D5" | "tetranor-12-HETE" | "TXB1" | "TXB2" | "TXB3"



## generic rules
FA = FA_pure | FA_pure Ether
FA_pure = Carbon Carbon_DB_separator DB | Carbon Carbon_DB_separator DB DB_hydroxyl_separator Hydroxyl | Carbon Carbon_DB_separator DB heavy | Carbon Carbon_DB_separator DB DB_hydroxyl_separator Hydroxyl heavy
Ether = "a" | "p"
LCB = Carbon Carbon_DB_separator DB DB_hydroxyl_separator Hydroxyl | Old_Hydroxyl Carbon Carbon_DB_separator DB
Carbon = Number
DB = DB_count | DB_count round_open_bracket DB_position round_close_bracket
DB_count = Number
DB_position = Number CisTrans | Number CisTrans | DB_position DB_position_separator DB_position
CisTrans = "E" | "Z"
Hydroxyl = Number
Old_Hydroxyl = "d" | "t"
Number =  "0" | "1" | "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | Number Number


heavy = "(+" isotopes  ")"
isotopes = isotopes isotopes | "[" Number "]" element Number
element = "C" | "H" | "O" | "N" | "P" | "S"

## separators
sorted_FA_separator = "/"
adduct_separator = " "
unsorted_FA_separator = "-"
headgroup_separator = " "
Carbon_DB_separator = ":"
DB_hydroxyl_separator = ";"
DB_position_separator = ","
round_open_bracket = "("
round_close_bracket = ")"