##################################################################################
# MIT License
# 
# Copyright (c) 2017 Dominik Kopczynski   -   dominik.kopczynski {at} isas.de
#                    Bing Peng   -   bing.peng {at} isas.de
#
# Permission is hereby granted, free of charge, to any person obtaining a copy
# of this software and associated documentation files (the "Software"), to deal
# in the Software without restriction, including without limitation the rights
# to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
# copies of the Software, and to permit persons to whom the Software is
# furnished to do so, subject to the following conditions:
# 
# The above copyright notice and this permission notice shall be included in all
# copies or substantial portions of the Software.
# 
# THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
# IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
# FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
# AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
# LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
# OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN THE
# SOFTWARE.
##################################################################################

## This is a BNF grammer for a lipid subspecies identifiers followed by
## J.K. Pauling et al. 2017, PLoS One, 12(11):e0188394. 

# first rule is always start rule
lipid = GL | PL | SL | Cholesterol | Mediator


## generic rules
FA = FApure | FApure Ether
FApure = Carbon ":" DB | Carbon ":" DB ";" Hydroxyl
Ether = "a" | "p"
LCB = Carbon ":" DB ";" Hydroxyl_LCB
Carbon = "2" | "3" | "4" | "5" | "6" | "7" | "8" | "9" | "10" | "11" | "12" | "13" | "14" | "15" | "16" | "17" | "18" | "19" | "20" | "21" | "22" | "23" | "24" | "25" | "26" | "27" | "28" | "29" | "30"
DB = "0" | "1" | "2" | "3" | "4" | "5" | "6"
Hydroxyl = "1" | "2" | "3" | "4" | "5" | "6"
Hydroxyl_LCB = "2" | "3"


## glycerolipid rules
GL = MGL | DGL | SGL | TGL
MGL = HG_MGL " " FA
DGL = HG_DGL " " FA unsorted_separator FA | HG_DGL " " FA sorted_separator FA
SGL = HG_SGL " " FA unsorted_separator FA | HG_SGL " " FA sorted_separator FA
TGL = HG_TGL " " FA unsorted_separator FA unsorted_separator FA | HG_DGL " " FA sorted_separator FA sorted_separator FA

HG_MGL = "MAG"
HG_DGL = "DAG"
HG_SGL = "MGAG" | "DGDG" | "SQDG"
HG_TGL = "TAG"


## phospholipid rules
PL = LPL | DPL | PL-O | CL | MLCL
PL-O = LPL-O | DPL-O
LPL = HG_LPL " " FA
LPL-O = HG_LPL-O " " FA
DPL = HG_PL " " FA unsorted_separator FA | HG_PL " " FA sorted_separator FA
DPL-O = HG_PL-O " " FA unsorted_separator FA | HG_PL-O " " FA sorted_separator FA
CL = HG_CL " " FA unsorted_separator FA unsorted_separator FA unsorted_separator FA | HG_CL " " FA sorted_separator FA sorted_separator FA sorted_separator FA
MLCL = HG_MLCL " " FA unsorted_separator FA unsorted_separator FA | HG_MLCL " " FA sorted_separator FA sorted_separator FA


HG_CL = "CL"
HG_MLCL = "MLCL"
HG_PL = "BMP" | "CDPDAG" | "DMPE" | "MMPE" | "PA" | "PC" | "PE" | "PEt" | "PG" | "PI" | "PIP" | "PIP2" | "PIP3" | "PS"
HG_LPL = "LPA" | "LPC" | "LPE" | "LPG" | "LPI" | "LPS"
HG_LPL-O = "LPC O" | "LPE O"
HG_PL-O = "PC O" | "PE O"


## sphingolipid rules
SL = LSL | DSL
LSL = HG_LSL " " LCB
DSL = HG_DSL " " LCB "/" FA

HG_LSL = "LCB" | "LCBP" | "LHexCer" | "LSM"
HG_DSL = "Cer" | "CerP" | "EPC" | "GB3" | "GB4" | "GD3" | "GM3" | "GM4" | "Hex2Cer" | "HexCer" | "IPC" | "M(IP)2C" | "MIPC" | "SHexCer" | "SM"


## cholesterol lipids
Cholesterol = CH | CHe
CH = "Ch"
CHe = "ChE " FApure


## mediator lipids
Mediator = "10-HDoHE" | "11-HDoHE" | "11-HETE" | "11,12-DHET" | "11(12)-EET"| "12-HEPE" | "12-HETE" | "12-HHTrE" | "12-OxoETE" | "12(13)-EpOME" | "13-HODE" | "13-HOTrE" | "14,15-DHET" | "14(15)-EET" | "14(15)-EpETE" | "15-HEPE" | "15-HETE" | "15d-PGJ2" | "16-HDoHE" | "16-HETE" | "18-HEPE" | "5-HEPE" | "5-HETE" | "5-HpETE" | "5-OxoETE" | "5,12-DiHETE" | "5,6-DiHETE" | "5,6,15-LXA4" | "5(6)-EET" | "8-HDoHE" | "8-HETE" | "8,9-DHET" | "8(9)-EET" | "9-HEPE" | "9-HETE" | "9-HODE" | "9-HOTrE" | "9(10)-EpOME" | "AA" | "alpha-LA" | "DHA" | "EPA" | "Linoleic acid" | "LTB4" | "LTC4" | "LTD4" | "Maresin 1" | "Palmitic acid" | "PGB2" | "PGD2" | "PGE2" | "PGF2alpha" | "PGI2" | "Resolvin D1" | "Resolvin D2" | "Resolvin D3" | "Resolvin D5" | "tetranor-12-HETE" | "TXB1" | "TXB2" | "TXB3"



## separators
sorted_separator = "/"
unsorted_separator = "-"